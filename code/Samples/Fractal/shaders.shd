//------------------------------------------------------------------------------
//  shaders.shd
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
@block util
_func vec4 hue2rgb(float h) {
    float r = abs(h * 6.0 - 4.0) - 1.0;
    float g = 2.0 - abs(h * 6.0 - 2.0);
    float b = 2.0 - abs(h * 6.0 - 4.0);
    return vec4(r, g, b, 1.0);
}
@end

//------------------------------------------------------------------------------
@vs MandelbrotVS
@in vec4 position
@in vec2 texcoord0
@uniform_block vsParams VSParams
    @uniform vec4 rect Rect
@end
@out vec2 uv
@out vec2 fractalPos
{
    _position = position;
    uv = texcoord0;
    fractalPos = rect.xy + (texcoord0 * (rect.zw - rect.xy));
}
@end

//------------------------------------------------------------------------------
@fs MandelbrotFS
@highp float
@uniform_block mandelbrotFSParams FSParams
    @uniform sampler2D tex Texture
@end
@in vec2 uv
@in vec2 fractalPos
{
    // read previous params
    vec4 params = tex2D(tex, uv);
    float cx = params.x;
    float cy = params.y;
    float i = params.w;
    float c = cx*cx + cy*cy;
    if (c > 4.0) {
        _color = vec4(cx, cy, 1.0, i);  // finished
    }
    else {
        float cxt = cx*cx - cy*cy + fractalPos.x;
        cy = 2.0 * cx*cy + fractalPos.y;
        cx = cxt;
        i += 1.0;
        _color = vec4(cx, cy, 0.0, i);
    }
}
@end

//------------------------------------------------------------------------------
@bundle Mandelbrot
@program MandelbrotVS MandelbrotFS
@end

//------------------------------------------------------------------------------
@vs DisplayVS
@in vec4 position
@in vec2 texcoord0
@out vec2 uv
{
    _position = position;
    uv = texcoord0;
}
@end

//------------------------------------------------------------------------------
@fs DisplayFS
@use util
@uniform_block fsParams FSParams
    @uniform sampler2D tex Texture
    @uniform float numColors NumColors
@end
@in vec2 uv
{
    vec4 params = tex2D(tex, uv);
    if (params.z > 0.5) {
        float i = params.w;
        float hue = mod(i, numColors) / numColors;
        _color = hue2rgb(hue);
    }
    else {
        _color = vec4(0.0, 0.0, 0.0, 1.0);
    }
}
@end

//------------------------------------------------------------------------------
@bundle Display
@program DisplayVS DisplayFS
@end